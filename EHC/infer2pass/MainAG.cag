%%[1
-------------------------------------------------------------------------
-- Main AG
-------------------------------------------------------------------------

{
module MainAG
  where

import IO
import Data.List
import UU.Pretty
import Infer2PassSupport
}
%%]

%%[1
INCLUDE "RulerInfer2Pass.ag"

WRAPPER AGItf
%%]

-------------------------------------------------------------------------
-- AST
-------------------------------------------------------------------------

%%[1
DATA AGItf
  | AGItf  e  : Expr
%%]

%%[1.AllAS
SET AllAS = Expr
%%]

%%[2.AllAS -1.AllAS
SET AllAS = Expr TyExpr
%%]

-------------------------------------------------------------------------
-- Initialisation
-------------------------------------------------------------------------

%%[1
SEM AGItf
  | AGItf  e  .   g  =   []
              .   c  =   []
%%]

-------------------------------------------------------------------------
-- Pretty printing
-------------------------------------------------------------------------

%%[1
ATTR AGItf AllAS [ | | pp: PP_Doc ]

SEM Expr
  | App  lhs  .   pp   =   @f.pp >#< pp_parens @a.pp
                           >-< mkErr @mtErrs
  | Int  lhs  .   pp   =   pp @int
  | Var  lhs  .   pp   =   pp @i >-< mkErr @nmErrs
  | Lam  lhs  .   pp   =   "\\" >|< pp @i >#< "->" >#< @b.pp
  | Let  lhs  .   pp   =   "let"     >#< pp @i
                           >#< "::"  >#< show @pty_e_
                           >#< "="   >#< @e.pp
                           >-< "in " >#< @b.pp
%%]

%%[2
SEM Expr
  | TLet lhs  .   pp   =   "let"     >#< pp @i
                           >#< "::"  >#< @te.pp
                           >#< "="   >#< @e.pp
                           >-< "in " >#< @b.pp
  | Char lhs  .   pp   =   pp @char

SEM TyExpr
  | Int  lhs  .   pp   =   pp "Int"
  | Char lhs  .   pp   =   pp "Char"
  | Var  lhs  .   pp   =   pp @i
  | All  lhs  .   pp   =   "forall" >#< @i >#< "." >#< @te.pp
  | Arr  lhs  .   pp   =   @a.pp >#< "->" >#< @r.pp
%%]

-------------------------------------------------------------------------
-- Uniq
-------------------------------------------------------------------------

%%[1
ATTR AllAS [ | uniq: UID | ]

SEM AGItf
  | AGItf  e  .   uniq  =   uidStart
%%]
